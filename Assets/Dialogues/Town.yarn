title: NeighborQuest
tags: neighbor's dialogue regarding butterfly infestation
position: 0, 0
---
<<declare $hasVisitedNeighbour = false as bool>>
<<declare $hasFoundTreeBranch = false as bool>>
<<declare $hasCapturedButterflies = false as bool>>
<<declare $hasFinishedNeighborQuest = false as bool>>

//talking to the neighbor for the first time
// i removed "&& $hasSpokenToDadAtHome" because a lot of people will miss talking to the dad at first and i don't want that to be a 'blocker'
<<if !$hasVisitedNeighbour>>
    Neighbor: You're here! Finally, finally!
    Iman: I am, I am! What's the matter?
    Neighbor: Butterflies, Iman! Butterflies everywhere!
    Iman: Yes, summer is coming, there are going to be butterflies.
    Neighbor: No, no. They're in my house! My house is full of butterflies! I have a chronic fear of them.
    Iman: Aren't you wearing a butterfly pattern?
    Neighbor: It's called exposure therapy! But anyways, what can I do? You're so smart, Iman. Surely you can help me…
    Iman: I don't really-- fine. Better this than dealing with another of dad's computer questions.
    Iman: Hm… Butterflies… Oh, there's this tree in the park, it grows flowers that attract butterflies!
    Iman: If I bring a branch, I could lead them out of the house.
    Neighbor: Yes, yes! Thank you!
    Iman: Well, off I go, I guess.
    <<set $hasVisitedNeighbour to true>>
    <<change_mission_prompt UIManager "Find the butterfly tree branch in the park">>
    <<stop>>
<<endif>>

//talking to neighbor before finding the branch
<<if !$hasFoundTreeBranch && $hasVisitedNeighbour>>
    Neighbor: Please come back with a branch from that blooming tree in the park!
<<endif>>

//talking to neighbor after finding the branch
<<if $hasFoundTreeBranch && !$hasCapturedButterflies>>
    Iman: Here's the branch.
    Neighbor: Wonderful, wonderful! Here, I'll open the door, and you get them out, yes?
    Iman: I mean, it's not going to happen by itself, right? That would be nice…
    Neighbor: ...
    Neighbor: Just give me that, please.
    <<set_grabbable "TREE BRANCH">>
    <<set_grabber NEIGHBOUR 0>>
    <<grab  "GRAB MANAGER">>
    <<change_mission_prompt UIManager "Now it is time to fix Dad's computer.">>
    <<set $hasCapturedButterflies to true>>
    <<stop>>
<<endif>>

//after helping neighbor with the butterflies
<<if $hasCapturedButterflies>>
    Neighbor: Iman, thank you, thank you!
    Iman: Don't worry about it.
    Neighbor: I can finally go back inside. Have a nice day, Iman!
    Iman: I feel like it's sarcastic, at this point. Well, time to go home.
    <<set $hasFinishedNeighborQuest to true>>
    <<stop>>
<<endif>>

//talking to neighbor after butterfly quest
<<if $hasFinishedNeighborQuest>>
    Neighbor: Thanks again!
<<endif>>
===

title: TreeBranch
position: 250, 0
---
<<if !$hasFoundTreeBranch>>
    <<if !$hasVisitedNeighbour>>
        // player has no knowledge of neighbour's problem
        Iman: Whoa, a butterfly tree. It is no joke, it attracks so many of them.
        Iman: Thinking of it, it is a bit disgusting.
        Iman: Didn't the neighbour say something about being scared of butterflies?
    <<else>>
        // player already heard mrs al qadiri complaints
        Iman: Oh, lucky! There's a branch already on the ground.
        Iman: ...
        Iman: I hope I won't get plagued by the butterflies myself now.
        <<set $hasFoundTreeBranch to true>>
        <<enable_trigger_events "TREE BRANCH" false>>
        <<set_grabbable "TREE BRANCH">>
        <<set_grabber "PLAYER IMAN" 0>>
        <<grab "GRAB MANAGER">>
        <<disable_player_lookat "TREE BRANCH">>
        Iman: Better bring this back before she has a heart attack!
        Iman: ...
        Iman: ...why am I doing this again?
        Iman: Oh, right. Avoiding Dad. And his broken computer.
        Iman: I'm sure he just forgot to plug it in, anyway.
        <<change_mission_prompt UIManager "Bring the branch back to avoid performing your duties.">>
    <<endif>>
<<endif>>
===

// <<set_grabbable "TREE BRANCH">>
// <<set_grabber IMAN index>>
// <<grab GrabManager>>

title: CatQuest
tags: Iman's dialogue regarding the cat
position: 0, 200
---
<<declare $hasEncounteredCat = false as bool>>
<<declare $hasGivenTuna = false as bool>>

<<if !$hasEncounteredCat>>
    <<if !$hasFoundTuna>>
        Cat: *mrew*
        Iman: Oh, kitty, kitty!
        Iman: Well, would you look at that?
        Iman: The cutest cat in the world has graced me with its presence!
        Iman: I should thank it with an offering. Hmmm…
        <<play_intonation_sound CAT 0>>
        Cat: *mrew?*
        Iman: I know! I'll go buy some tuna. Cats love tuna, right? I'll just hop by the market.
        Iman: I'm definitely not using this as an excuse to do some shopping.
        <<set $hasEncounteredCat to true>>
        <<change_secondary_mission_prompt UIManager "Find something for the cat to eat. Like tuna!">>
        <<stop>>
    <<else>> // has found tuna before encountering cat first time
        Cat: *MEOW! MEOW! MEOW!*
        Iman: Dear lord, what is going with you?
        <<play_intonation_sound CAT 0>>
        Cat: *MEEEEOW! MEOW MEOW! MEOOW! MEOOOOOOOOOOOOW!!!*
        Iman: Do you really like me that much?
        <<play_intonation_sound CAT 1>>
        Cat: *purrrrrrr purrrrrrrr purrrrr*
        Iman: Awww, cutie pie! Have some pets!
        <<play_intonation_sound CAT 3>>
        Cat: *hiss!*
        Iman: Hey!
        <<play_intonation_sound CAT 1>>
        Cat: *purrrrs purrrrs purrrrrrrrrrrs*
        Iman: Oh, I see.
        Iman: Of course, that wasn't because of my great looks.
        Iman: I had that tuna can on me.
        Iman: Take it if that makes you so happy!
        <<play_intonation_sound CAT 1>>
        Cat: *purrs furiously*
        <<set $hasGivenTuna to true>>
        <<set $hasEncounteredCat to true>>
        <<change_secondary_mission_prompt UIManager "">>
        <<stop>>
    <<endif>>
<<else>> // has already encountered cat
    <<if !$hasFoundTuna>>
        Iman: No, I just can't leave this poor cat to starve!
        Iman: Also, my wallet is burning with buyer's fever.
        Iman: To the market!
    <<else>> // has found tuna after having encountered cat first time
        Iman: Here you go, kitty.
        <<set $hasGivenTuna to true>>
        <<change_secondary_mission_prompt UIManager "">>
    <<endif>>
<<endif>>
===

title: TunaCan
tags: When interacting with the tuna
position: 250, 200
---
<<declare $hasFoundTuna = false as bool>>

<<if !$hasFoundTuna>>
    <<if !$hasEncounteredCat>>
        // player knows nothing about cats or tuna cans
        Iman: What is this?
        Iman: "Tuna, Import Type (super tasty)"
        Iman: People really leave crazy things on the floor.
    <<else>>
        // player has encountered cat already, but has no prior knowledge of the tuna can
        Iman: Oh, how curious.
        Iman: So that tuna can has been left there just for my own convenience.
        Iman: Lol I don't think so.
        Iman: But it's marked "import type", which is kinda neat.
        Game Manager: You got: 1 tuna can(s).
        Iman: … What?
    <<endif>>
    <<set $hasFoundTuna to true>>
    <<disable_player_lookat "TUNA CAN">>
    <<disable_tuna_can "TUNA CAN">>
    <<change_secondary_mission_prompt UIManager "Great powers mean great responsibilities. Great tuna means... a great kitten?">>
<<endif>>
===

title: VetSermon
tags: When interacting with the tuna
position: 0, 400
---
<<declare $hasInterruptedCatFeeding = false as bool>>

<<if $hasGivenTuna>>
    <<if $hasInterruptedCatFeeding>>
        <<enable_look_at VETERINARIAN>>
        Vet: No need to worry.
    <<else>>
    <<enable_look_at VETERINARIAN>>
    Vet: Hey!
    Vet: You can't give tuna to that cat!
    Iman: What, why? Don't cats like fish?
    Vet: They do, but this is tuna. They might like the strong smell, but it's not a well-balanced food.
    Vet: Also, this cat is allergic to tuna.
    Iman: Oh, no!
    Vet: It's fine, it just means there's going to be some hair loss and a ton of farting.
    Vet: I'm bringing it for observation, but no need to worry.
    Iman: Okay… Goodbye, doctor. Sorry, kittycat.
    <<set $hasInterruptedCatFeeding to true>>
    <<endif>>
<<else>>
    Vet: ...
    Iman: May I ask you, what have you been looking at for so long?
    Vet: The creeping void of absolute nothingness.
    Iman: Oh dear.
<<endif>>
===

title: NorthWestPathBlocking
tags: path blocking
---
Iman: Oh.
Iman: The mountain part of the desert is from here.
Iman: I don't want to end up being a fried stick.
Iman: Better stay in town!
===

title: SouthWestPathBlocking
tags: path blocking
---
Iman: From here, the endless, glorious desert!
Iman: Not that there is much to do with all this sand.
Iman: And I promised I was going to take care of all those things.
Iman: Ugh.
===

title: SouthEastPathBlocking
tags: path blocking
---
Iman: This is such a lovely part of the mountain.
Iman: I remember camping here with Mom and Dad.
Iman: It would be nice doing that again, one day.
Iman: But now there is a lot left to do.
===

title: EastPathBlocking
tags: path blocking
---
Iman: Well, the roads of all adventures.
Iman: Also, Nour's street.
Iman: I really have to finish what I have to do first.
===